apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
    }
}


android {

    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.fyber.sampleapp"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 1
        versionName "1.0"
    }

    signingConfigs {
        release {
            storeFile file(keyStoreFile)
            storePassword keyStorePassword
            keyAlias keyStoreAlias
            keyPassword keyStoreAliasPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {

        internal {
            ext.enableCrashlytics = false
        }

        //flavour for crowd testing.
        // Includes Crashlytics and Mediation.
        // Some mediation proguard specific entries as well
        alpha {

            proguardFile 'src/alpha/proguard-rules.pro'

            dependencies {
                //we include crashlytics only for the alpha crowd testing program flavor
                alphaCompile('com.crashlytics.sdk.android:crashlytics:2.5.0@aar') {
                    transitive = true;
                }
                //crashlytics group name
                ext.betaDistributionGroupAliases = "delta"

                //mediation dependencies
                provided 'com.fyber:sdk-annotations:1.0.0-rc2'
                provided 'com.fyber:sdk-annotations-processor:1.0.0-rc2'

                alphaCompile 'com.squareup.dagger:dagger:1.2.2'
                //adapters
                alphaCompile 'com.fyber:fyber-mediation-adcolony:2.2.2-r3'
                alphaCompile 'Adapters-Android:fyber-mediation-admob:7.5.0-r2'
                alphaCompile 'Adapters-Android:fyber-mediation-applovin:6.0.0-r2'
                alphaCompile 'Adapters-Android:fyber-mediation-chartboost:5.5.0-r2'
                alphaCompile 'Adapters-Android:fyber-mediation-facebook:4.3.0-r2'
                alphaCompile 'Adapters-Android:fyber-mediation-flurry:5.5.0-r3'
                alphaCompile 'Adapters-Android:fyber-mediation-inmobi:4.5.5-r2'
                alphaCompile 'Adapters-Android:fyber-mediation-mediabrix:1.7.1-r2'
                alphaCompile 'Adapters-Android:fyber-mediation-millennial:5.4.0-r3'
                alphaCompile 'Adapters-Android:fyber-mediation-tremor:3.10.0.22-r2'
//                alphaCompile 'Adapters-Android:fyber-mediation-unityads:1.4.4-r2'
                alphaCompile 'Adapters-Android:fyber-mediation-vungle:3.3.1-r2'

            }
        }

        applicationVariants.all { variant ->
            if (buildType.name.equals('release')) {
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def fileName = outputFile.name.replace('release.apk', "${versionName}.apk")
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //Google libs
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.google.android.gms:play-services-ads:7.5.0'
    //Fyber SDK
    compile 'com.fyber:fyber-android-sdk:8.0.0-GM2'
    //3rd party lib for view dependencies injection
    compile 'com.jakewharton:butterknife:7.0.1'
    //if you want to use mediation you must uncomment the following dependencies:
    //Also you have to uncomment the @FyberSDK annotation in the MainActivity
    //    provided 'com.fyber:sdk-annotations:1.0.0-rc2'
    //    provided 'com.fyber:sdk-annotations-processor:1.0.0-rc2'
}